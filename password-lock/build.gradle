buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
        classpath "com.github.dcendents:android-maven-gradle-plugin:1.3"
    }
}
apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

repositories {
    jcenter()
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.1.1'
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
    }
}

group = 'com.engapps'
version = '0.1.0'

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                groupId project.group
                artifactId 'password-lock'

                // Add your description here
                name 'password-lock'
                description 'Android library to add a password lock screen.'
                url 'https://github.com/jfbibeau/password-lock'

                licenses {
                    license {
                        name 'MIT'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id 'jfbibeau'
                        name 'Jean-Francois Bibeau'
                        email 'jfbibeau@gmail.com'
                    }
                }
                scm {
                    connection 'https://github.com/jfbibeau/password-lock.git'
                    developerConnection 'https://github.com/jfbibeau/password-lock.git'
                    url 'https://github.com/jfbibeau/password-lock'
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'password-lock'
        desc = 'Android library to add a password lock screen.'
        websiteUrl = 'https://github.com/jfbibeau/password-lock'
        vcsUrl = 'https://github.com/jfbibeau/password-lock.git'
        licenses = ["MIT"]
        publish = true
        publicDownloadNumbers = true
        version {
            desc = 'Android library to add a password lock screen.'
        }
    }
}
